apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

apply from: './config/signing.gradle'
apply from: './config/versions.gradle'
apply from: './config/sdk.gradle'
apply from: './config/lint.gradle'
apply from: './config/packaging.gradle'

apply plugin: 'kotlin-kapt'

import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

tasks.withType(KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
    }
}

android {
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    dexOptions {
        preDexLibraries true
    }

    def proGuardFileCollection = files(
        file("../proguard").listFiles(),
        getDefaultProguardFile('proguard-android.txt')
    )

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false

            buildConfigField "String", "BASE_URL", "\"https://avenue-for-the-arts-backend.herokuapp.com/\""
            buildConfigField "String", "VENUE_MAP_URL", "\"https://www.google.com/maps/d/u/0/viewer?mid=1qx2B4eYIFcgWSOJKGsaKDoV0Z44&ll=42.95960771248584%2C-85.66844540535385&z=16\""

            resValue "string", "google_maps_key", "AIzaSyD6N_6-RoBEtKo9eXu3op8kS2sIgy-0Czk"
            resValue "string", "account_type", android.defaultConfig.applicationId + applicationIdSuffix
            resValue "string", "content_authority", android.defaultConfig.applicationId + ".provider"
        }
        debugMinified.initWith(buildTypes.debug)
        debugMinified {
            minifyEnabled true
            shrinkResources true
            proguardFiles proGuardFileCollection.getFiles().toArray()
        }
        release {
            minifyEnabled false
            shrinkResources false
            debuggable false
            proguardFiles proGuardFileCollection.getFiles().toArray()

            buildConfigField "String", "BASE_URL", "\"https://avenue-for-the-arts-backend.herokuapp.com\""
            buildConfigField "String", "VENUE_MAP_URL", "\"https://www.google.com/maps/d/u/0/viewer?mid=1qx2B4eYIFcgWSOJKGsaKDoV0Z44&ll=42.95960771248584%2C-85.66844540535385&z=16\""

            resValue "string", "google_maps_key", "AIzaSyDS0inkMeNebXpFylzXw9y_TQndNRFUJqs"
            resValue "string", "account_type", android.defaultConfig.applicationId
            resValue "string", "content_authority", android.defaultConfig.applicationId + ".provider"
        }
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    google()
}

dependencies {
    // Support library
    def supportVersion = "27.0.1"
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:recyclerview-v7:$supportVersion"
    implementation "com.android.support:support-v4:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:cardview-v7:$supportVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:multidex:1.0.2'
    implementation "com.google.android.gms:play-services-maps:11.6.0"

    // Dependency injection
    def daggerVersion = "2.12"
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    def ankoVersion = "0.10.2"
    implementation "org.jetbrains.anko:anko-sdk21:$ankoVersion"
    implementation "org.jetbrains.anko:anko-support-v4:$ankoVersion"
    implementation "org.jetbrains.anko:anko-appcompat-v7:$ankoVersion"

    // HTTP
    def retrofitVersion = "2.3.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:3.9.0"

    // Rx
    implementation 'io.reactivex.rxjava2:rxjava:2.1.6'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // Logging
    implementation 'com.jakewharton.timber:timber:4.6.0'

    // Date/Time
    implementation 'net.danlew:android.joda:2.9.9'

    // Images
    implementation 'com.squareup.picasso:picasso:2.5.2'

    implementation 'com.afollestad:sectioned-recyclerview:0.5.0'

    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }
}
